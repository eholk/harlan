;; A Mandelbrot written in a more modern Harlan.

(module
  (import graphics)

  (extern nanotime () -> u64)

  (define-datatype Complex
    (complex float float))

  (define (complex-add a b)
    (match a
      ((complex ar ai)
       (match b
         ((complex br bi)
          (complex (+ ar br) (+ ai bi)))))))

  (define (complex-mul a b)
    (match a
      ((complex ar ai)
       (match b
         ((complex br bi)
          (complex (- (* ar br) (* ai bi))
                   (+ (* ar bi) (* ai br))))))))
  
  (define (complex-mag2 c)
    (match c
      ((complex r i)
       (+ (* r r) (* i i)))))
  
  (define (main)
    (let ((start (nanotime)))
      (let ((img (kernel ((i (iota 1024)))
                   (kernel ((j (iota 1024)))
                     (let ((c (complex
                               (- (/ (int->float j) 512.0) 1.5)
                               (- (/ (int->float i) 512.0) 1.0)))
                           (x (complex 0 0))
                           (idx 0)
                           (escape 0))
                       (while (< idx 256)
                         (set! x (complex-add (complex-mul x x) c))
                         (if (< (complex-mag2 x) 4.0)
                             (set! escape idx))
                         (set! idx (+ idx 1)))
                       escape)))))
        (let ((stop (nanotime)))
          (write-pgm "test.bin/mandelbrot.pgm" img)
          (print "Time to generate Mandelbrot Set in milliseconds:\n")
          (print (/ (- stop start) 1000000))
          (print "\n")
          (return 0))))))
