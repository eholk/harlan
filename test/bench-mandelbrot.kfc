;; This no longer bothers saving the image. The other mandelbrot test
;; makes sure that works.

(%testspec
 (%tags bench))

(module
  (extern nanotime () -> u64)

  (define (mandelbrot max_iter)
    (let ((start (nanotime)))
      (let ((imt (kernel ((i (iota 1024)))
                   (kernel ((j (iota 1024)))
                     (let ((cr
                            (+ (* (/ (int->float j) 512.0)
                                  0.000009349778)
                               -1.373120668462))
                           (ci
                            (+ (* (/ (int->float i) 512.0)
                                  0.000009349778)
                               -0.084468836480))
                           (xr 0.0)
                           (xi 0.0)
                           (idx 0)
                           (escape 0))
                       (while (< idx max_iter)
                         (let ((xrp (+ (- (* xr xr) (* xi xi)) cr))
                               (xip (+ (* 2.0 (* xr xi)) ci)))
                           (set! xr xrp)
                           (set! xi xip)
                           (let ((m (+ (* xr xr) (* xi xi))))
                             (if (< m 4.0)
                                 ;; Why do the pixels all turn white
                                 ;; if I just do (set! escape idx)?
                                 (set! escape (- idx 1))))
                           (set! idx (+ idx 1))))
                       (/ (* escape 256) max_iter))))))
        (let ((stop (nanotime)))
          (- stop start)))))
    
  (define (iterate)
    (for (i 1 50)
      (let ((max_iter (* i 100)))
        (let ((elapsed (mandelbrot max_iter)))
          (print max_iter)
          (print "\t")
          (println (/ elapsed 1000000))))))

  (define (main)
    (iterate)
    0))

