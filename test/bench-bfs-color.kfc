;; Breadth first search

(%testspec
 (%tags bench))

(module
  (import random)

  (define (elapsed-sec start end)
    (/ (u64->float (- end start)) 1e9))

  (define-datatype color
    (white)
    (gray)
    (black))

  (define (color-eqp c1 c2)
    (match c1
      ((white) (match c2
                 ((white) #t)
                 ((gray)  #f)
                 ((black) #f)))
      ((gray)  (match c2
                 ((white) #f)
                 ((gray)  #t)
                 ((black) #f)))
      ((black) (match c2
                 ((white) #f)
                 ((gray)  #f)
                 ((black) #t)))))
  
  (define-macro append ()
    ((_ A B)
     (let ((a A) (b B))
       (let ((lenA (length a))
             (len (+ (length a) (length b))))
         (kernel ((i (iota len)))
           (if (< i lenA)
               (vector-ref a i)
               (vector-ref b (- i lenA))))))))

  ;; FIXME: We'd like to call choose-edges from make-graph-inner, but
  ;; calling functions that have kernels from within a kernel is
  ;; currently broken. Depends on issue #114.
  ;;
  ;;(define (choose-edges N max_vertex rng)
  ;;  (kernel ((i (iota N)))
  ;;    (let ((rng (reseed rng i)))
  ;;      (match (random rng)
  ;;        ((RandomResult n _)
  ;;         (mod n max_vertex))))))

  (define (make-graph-inner edge_factor N rng)
    (kernel ((i (iota N)))
      (let ((rng (reseed rng i))
            (N edge_factor)
            (max_vertex N))
        (kernel ((i (iota N)))
          (let ((rng (reseed rng i)))
            (match (random rng)
              ((RandomResult n _)
               (mod n max_vertex))))))))

  (define (make-graph edge_factor N)
    (make-graph-inner edge_factor N (init-rand)))

  ;; Ideally, this would be make-graph, but we'd have to be able to do
  ;; function calls from kernels first.
  ;;(define (make-graph edge_factor N)
  ;;  (kernel ((i (iota N))) (choose_edges edge_factor N)))

  (define (iterate-colors graph colors)
    (println "iterate")
    (let ((new-colors
           (kernel ((i (iota (length graph))) (c colors))
             (match c
               ((black) (black))
               ((gray)  (black))
               ((white)
                (if (reduce or (kernel ((j (vector-ref graph i)))
                                 (match (vector-ref colors j)
                                   ((white) #f)
                                   ((gray)  #t)
                                   ((black) #f))))
                    (gray)
                    (white)))))))
      (println "computed new colors")
      (if (reduce and (kernel ((c1 new-colors)
                               (c2 colors))
                        (color-eqp c1 c2)))
          colors
          (begin
            ;;(println new-colors)
            (iterate-colors graph new-colors)))))

  (define (bfs graph start)
    (let ((colors (kernel ((i (iota (length graph))))
                    (if (= i start)
                        (gray)
                        (white)))))
      ;;(println colors)
      (iterate-colors graph colors)))
  
  (define (main)
    (let* ((args (command-line))
           (N (if (> (length args) 1)
                  (parse-str->int (vector-ref args 1) 100)
                  100))
           (edge-factor (if (> (length args) 2)
                            (parse-str->int (vector-ref args 2) 16)
                            16))
           (start (time-s))
           (graph (make-graph edge-factor N))
           (stop  (time-s)))
      (println* "Generated graph with " (length graph) " vertices, edge-factor "
                 edge-factor " in "(- stop start) " seconds.")
      (let* ((start  (nanotime))
             (result (bfs graph 0))
             (stop   (nanotime)))
        (println* "Performed BFS in " (elapsed-sec start stop) " seconds.")
        (println* "SELFTIMED: " (elapsed-sec start stop))
        ))
    0)
  
  )
