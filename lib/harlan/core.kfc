;; This is the core module. It is loaded into every Harlan program.

(module

  (extern open_outfile (str) -> (ptr ofstream))
  (extern close_outfile ((ptr ofstream)) -> void)

  (define-macro cond (else)
    ((_ (else body))
     body)
    ((_ (test body))
     (if test body))
    ((_ (test body) rest ...)
     (if test body (cond rest ...))))

  (define-macro let* ()
    ((_ () b ...) (begin b ...))
    ((_ ((x e) (x* e*) ...) b ...)
     (let ((x e))
       (let* ((x* e*) ...) b ...))))

  (define-macro for ()
    ((_ (i start stop) body ...)
     (for (i start stop 1) body ...))
    ((_ (i start stop step) body ...)
     (let ((i start)
           (stepv step))
       (while (< i stop)
         body ...
         (set! i (+ i stepv))))))
  
  (define (write-pgm file data)
    (let ((stream (open_outfile file))
          (rows (length data))
          (cols (length (vector-ref data 0))))
      (print "P2\n" stream)
      (print rows stream)
      (print " " stream)
      (print cols stream)
      (print "\n" stream)
      (print "255\n" stream)
      (for (i 0 rows 1)
        (for (j 0 cols 1)
          (let ((p (vector-ref (vector-ref data i) j)))
            (print (cond
                     ((< p 0)   0)
                     ((> p 255) 255)
                     (else p))
                   stream)
            (print " " stream))))
      (close_outfile stream)))

  (define-macro inner-reduce (kernel)
    ;; This first one fuses reduce of a kernel.
    ((_ op (kernel ((x e) (x* e*) ...) b))
     (let ((x e) (x* e*) ...)
       (let ((t (let ((x (vector-ref x 0))
                      (x* (vector-ref x* 0)) ...)
                  b)))
         (for (reduce-index 1 (length x))
           (set! t (op t (let ((x (vector-ref x reduce-index))
                               (x* (vector-ref x* reduce-index)) ...)
                           b))))
         t)))
    ((_ op e)
     (let ((x e))
       (let ((t (vector-ref x 0)))
         (for (i 1 (length x))
           (set! t (op t (vector-ref x i))))
         t))))
  
  (define-macro reduce (kernel)
    ;; Comment out this next clause to enable faster reduction.
    ;;((_ op e)
    ;; (inner-reduce op e))
    ((_ op (kernel ((x e) (x* e*) ...) b))
     (let ((x e)
           (stride 65536))
       (if (< (length x) stride)
           (inner-reduce op (kernel ((x x) (x* e*) ...) b))
           (inner-reduce
            op
            (let ((x e) (x* e*) ...)
              (kernel ((i--- (iota stride)))
                (let ((t (let ((x (unsafe-vector-ref x i---))
                               (x* (unsafe-vector-ref x* i---)) ...)
                           b)))
                  (for (reduce-index (+ i--- stride) (length x) stride)
                    (set!
                     t
                     (op t
                         (let ((x (unsafe-vector-ref x reduce-index))
                               (x* (unsafe-vector-ref x* reduce-index)) ...)
                           b))))
                  t)))))))
    ((_ op e)
     (let ((x e)
           (stride 65536))
       (if (< (length x) stride)
           (inner-reduce op x)
           (let ((t--- (kernel ((i--- (iota stride)))
                         (let ((tmp--- (unsafe-vector-ref x i---)))
                           (for (j--- (+ i--- stride) (length x) stride)
                             (set! tmp---
                                   (op tmp--- (unsafe-vector-ref x j---))))
                           tmp---))))
             (inner-reduce op t---))))))
  )
